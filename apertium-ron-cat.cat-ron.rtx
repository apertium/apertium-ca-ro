! POS

adj: _.adj_type.gender.number.case.definite ;
adj_conc: (if((1.gender_chunk=m | 1.gender_chunk=nt) & 1.number=sg) 1(adj)[gender=mn] elif((1.gender_chunk=f | 1.gender_chunk=nt) & 1.number=pl) 1(adj)[gender=fn] else 1(adj) ) ;
adv: _.adv_type ;
det: _.adj_type.gender.number.case ;
det_conc: (if((1.gender_chunk=m | 1.gender_chunk=nt) & 1.number=sg) 1(det)[gender=mn] elif((1.gender_chunk=f | 1.gender_chunk=nt) & 1.number=pl) 1(det)[gender=fn] else 1(det) ) ;
n: _.acr.gender.number.case.definite ;
np: _.np_type.gender.number.case.definite ;
pr: _.gen ;
vbavea: _.tense.person.number ;
vbhaver: _.tense.person.number ;
vblex: _.tense.person.number ;
vbmod: _.tense.person.number ;
vbser: _.tense.person.number ;
vb_impers: _.tense ;
vb_pp: _.tense.gender.number ;
vb_pp_conc: (if(1.gender=m & 1.number=sg) 1(vb_pp)[gender=mn] elif(1.gender=f & 1.number=pl) 1(vb_pp)[gender=fn] else 1(vb_pp) ) ;

SAdj: _.adj_type.gender_chunk.number.case.definite ;
SAdv: _.adv_type ;
SDet: _.adj_type.gender_chunk.number.case ;
SN: _.SN_det.np_type.person.gender_chunk.number.case.definite ;
SPrep: _.gen ;
SV: _.vb_type.vb_cnj.tense.person.number ;



! Attributes

acr = acr ;
adj_type = def dem ord pos ;
adv_type = itg ;
case = nom dg ;
definite = ind def ;
gen = gen ;
gender = (GD m) m f nt mn fn @mf @mfn GD ;
gender_chunk = (GD m) m f nt GD ;
np_type = ant cog top al ;
number = (ND sg) sg pl @sp ND ;
person = (PD p3) p1 p2 p3 PD ;
tense = pri fti cni imp prs pii ifi inf ger pp ;
vb_type = vbavea vblex vbmod vbser ;
vb_cnj = cnj impers ;

SN_det = undet det predet nodet prnsubj ;

gender>gender_chunk: mf GD, mfn GD, mn m, fn f ;
gender_chunk>gender: GD GD ;


! Rules

SAdj -> 1: %adj
                [$case=nom,$definite=ind]
                { %1(adj_conc) } ;


SAdv -> 1: %adv
                { 1 } ;


SDet -> 1: %det.def
                [$case=nom]
                { %1(det_conc) } |
        1: %det.pos
                [$case=nom]
                { %1(adj_conc)[definite=ind] } |
        2: %adj.pos
                [$case=nom]
                { %1(adj_conc)[definite=ind] } |
        1: %det.dem
                [$case=nom]
                { %1(det_conc)} ;


SN ->   1: %n
                [$SN_det=undet,$case=nom,$definite=ind]
                { %1 } |

        1: %np.ant.m
                [$SN_det=det,$case=nom,$definite=def]
                { (if($case=dg) [lui@adj.pos.mn.sg.nom.def _] ) %1[case=nom] } |
        1: %np.ant.f
                [$SN_det=det,$case=nom,$definite=def]
                { (if(1.gen not=gen & $case=dg) [lui@adj.pos.mn.sg.nom.def _ %1[case=nom]] else %1 ) } |
        1: %np.cog
                [$SN_det=det,$case=nom,$definite=def]
                { (if($case=dg) [lui@adj.pos.mn.sg.nom.def _] ) %1[case=nom] } |
        1: %SN.det.ant SN.det.cog
                { %1 _1 2 } |
        1: %np.top
                [$SN_det=det,$case=nom,$definite=def]
                { %1 } |

        2: %SN SPrep
                { %1 _1 2 } |

        2: %SN SAdj
                { %1 _1 2[gender=$gender,number=$number] } |

        1: SDet.def %SN.undet
                [$SN_det=det]
                { %2[definite=def] } |
        1: SDet.dem %SN.undet
                [$SN_det=det]
                { %1 _1 2[gender=$gender,number=$number] } |
        3: SDet.pos %SN.undet
                [$SN_det=det]
                { %2[definite=def] _1 1[gender=$gender,number=$number] } |
        3: SDet.pos %SN.undet SPrep
                [$SN_det=det]
                { %2[definite=def] _1 1[gender=$gender,number=$number] _2 3 } |
        3: %SN.undet SDet.pos 
                [$SN_det=det]
                { %1[definite=def] _1 2[gender=$gender,number=$number] } ;


SPrep -> 0: %pr
                { 1 } |

         0: %de@SPrep SN
                { 2[case=dg] } |
         1: %SPrep SN
                { 1 _1 2[definite=(if(1.lem=cu) def else 2.definite)] } |
         2: %SPrep.gen SN
                { 1 _1 2[case=dg] } ;


SV ->   1: %vblex.pri
                [$vb_cnj=cnj]
                { %1 } |
        1: %vblex.fti
                [$vb_cnj=cnj]
                { vrea@vaux.pri.$person.$number _ 1(vb_impers)[tense=inf] } |
        1: %vblex.cni
                [$vb_cnj=cnj]
                { avea@vbavea.$tense.$person.$number _ 1(vb_impers)[tense=inf] } |
        1: %vblex.pis
                [$vb_cnj=cnj,$tense=pri]
                { %1 } |
        1: %vblex.imp
                [$vb_cnj=cnj,$person=(if(1.person=p3 & 1.number=pl) p2 else 1.person)] ! cantin > cântați
                { %1 } |
        1: %vblex.prs
                [$vb_cnj=cnj]
                { %1 } |
        1: %vblex.pii
                [$vb_cnj=cnj]
                { %1 } |
        1: %vblex.ifi
                [$vb_cnj=cnj]
                { %1 } |

        1: %vblex.inf
                [$vb_cnj=impers]
                { (if($tense=pp) %1(vb_pp_conc) else %1(vb_impers)) } |
        1: %vblex.ger
                [$vb_cnj=impers]
                { %1(vb_impers) } |
        1: %vblex.pp
                [$vb_cnj=impers]
                { %1(vb_pp_conc) } |

        1: %vbser.pri
                [$vb_cnj=cnj]
                { %1 } |
        1: %vbser.fti
                [$vb_cnj=cnj]
                { vrea@vaux.pri.$person.$number _ 1(vb_impers)[tense=inf] } |
        1: %vbser.cni
                [$vb_cnj=cnj]
                { avea@vbavea.$tense.$person.$number _ 1(vb_impers)[tense=inf] } |
        1: %vbser.pis
                [$vb_cnj=cnj,$tense=pri]
                { %1 } |
        1: %vbser.imp
                [$vb_cnj=cnj,$person=(if(1.person=p3 & 1.number=pl) p2 else 1.person)] ! cantin > cântați
                { %1 } |
        1: %vbser.prs
                [$vb_cnj=cnj]
                { %1 } |
        1: %vbser.pii
                [$vb_cnj=cnj]
                { %1 } |
        1: %vbser.ifi
                [$vb_cnj=cnj]
                { %1 } |

        1: %vbser.inf
                [$vb_cnj=impers]
                { (if($tense=pp) %1(vb_pp_conc) else %1(vb_impers)) } |
        1: %vbser.ger
                [$vb_cnj=impers]
                { %1(vb_impers) } |
        1: %vbser.pp
                [$vb_cnj=impers]
                { %1(vb_pp_conc) } |

        1: %vbhaver.pri
                [$vb_cnj=cnj]
                { %1 } |
        1: %vbhaver.fti
                [$vb_cnj=cnj]
                { vrea@vaux.pri.$person.$number _ fi@vbser.inf } |
        1: %vbhaver.cni
                [$vb_cnj=cnj]
                { %1 } |
        1: %vbhaver.pis
                [$vb_cnj=cnj,$tense=pri]
                { %1 } |
        1: %vbhaver.prs
                [$vb_cnj=cnj,$tense=pri]
                { %1 } |
        1: %vbhaver.pii
                [$vb_cnj=cnj,$tense=pri]
                { %1 } |
        1: %vbhaver.ifi
                [$vb_cnj=cnj,$tense=pri]
                { %1 } |

        1: %vbhaver.inf
                [$vb_cnj=impers]
                { %1(vb_impers) } |
        1: %vbhaver.ger
                [$vb_cnj=impers]
                { %1(vb_impers)[tense=inf] } |
        1: %vbhaver.pp
                [$vb_cnj=impers]
                { %1(vb_impers)[tense=inf] } |

        1: %vbmod.pri
                [$vb_cnj=cnj]
                { (if(1.lem equalcl trebui) %1[person=p3,number=sg] else %1) } |
        1: %vbmod.fti
                [$vb_cnj=cnj]
                { vrea@vaux.pri.$person.$number _ 1(vb_impers)[tense=inf] } |
        1: %vbmod.cni
                [$vb_cnj=cnj]
                { avea@vbavea.$tense.$person.$number _ 1(vb_impers)[tense=inf] } |
        1: %vbmod.pis
                [$vb_cnj=cnj,$tense=pri]
                { (if(1.lem equalcl trebui) %1[person=p3,number=sg] else %1) } |
        1: %vbmod.imp
                [$vb_cnj=cnj,$person=(if(1.person=p3 & 1.number=pl) p2 else 1.person)] ! cantin > cântați
                { (if(1.lem equalcl trebui) %1[person=p3,number=sg] else %1) } |
        1: %vbmod.prs
                [$vb_cnj=cnj]
                { (if(1.lem equalcl trebui) %1[person=p3,number=sg] else %1) } |
        1: %vbmod.pii
                [$vb_cnj=cnj]
                { (if(1.lem equalcl trebui) %1[person=p3,number=sg] else %1) } |
        1: %vbmod.ifi
                [$vb_cnj=cnj]
                { (if(1.lem equalcl trebui) %1[person=p3,number=sg] else %1) } |

        1: %vbmod.inf
                [$vb_cnj=impers]
                { (if($tense=pp) %1(vb_pp_conc) else %1(vb_impers)) } |
        1: %vbmod.ger
                [$vb_cnj=impers]
                { %1(vb_impers) } |
        1: %vbmod.pp
                [$vb_cnj=impers]
                { %1(vb_pp_conc) } |

        2: %merge@SV.vblex.cnj.pri SV.*.impers.inf
                [$lem=2.lem,$tense=ifi]
                { avea@vbavea.pri.$person.$number _1 2[tense=pp] } |
        2: %avea@SV.vbavea.cnj.pri SV.*.impers.pp
                [$lem=2.lem,$tense=ifi]
                { 1[person=$person,number=$number] _1 2 } ;
